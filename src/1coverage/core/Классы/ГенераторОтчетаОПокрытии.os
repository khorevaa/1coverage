
Перем ДанныеПокрытия;

Процедура ПриСозданииОбъекта(пДанныеПокрытия) Экспорт

	ДанныеПокрытия = пДанныеПокрытия;

КонецПроцедуры

Процедура СохранитьОтчетВФорматеВФайл(ФорматОтчета, ПутьКФайлу) Экспорт

	Отчет = СгенерироватьОтчетВФормате(ФорматОтчета);
	СохранитьОтчетВФайл(Отчет, ПутьКФайлу);

КонецПроцедуры

Функция СгенерироватьОтчетВФормате(ФорматОтчета)

	Если ФорматОтчета = "GenericCoverage" Тогда
		ГенераторОтчета = Новый ГенераторОтчетаGenericCoverage(ДанныеПокрытия);
	ИначеЕсли ФорматОтчета = "lcov" Тогда
		ГенераторОтчета = Новый ГенераторОтчетаlcov(ДанныеПокрытия);
	Иначе
		ВызватьИсключение("Неизвестный формат отчета");
	КонецЕсли;

	Возврат ГенераторОтчета.СгенерироватьОтчет();

КонецФункции

Процедура СохранитьОтчетВФайл(Отчет, ПутьКФайлу)

	ФайлРезультата = Новый Файл(ПутьКФайлу);
	Если Не ФайлРезультата.Существует() Тогда
		СоздатьКаталог(ФайлРезультата.Путь);
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(Отчет);
	ТекстовыйДокумент.Записать(ПутьКФайлу);

КонецПроцедуры
