#Использовать fs

Перем Лог;

Перем ДанныеПокрытия;

Процедура ПриСозданииОбъекта(пДанныеПокрытия) Экспорт

	Лог = ПараметрыПриложения.Лог();

	ДанныеПокрытия = пДанныеПокрытия;

КонецПроцедуры

Процедура СгенерироватьОтчет(ПутьКФайлу) Экспорт

	Файл = Новый Файл(ПутьКФайлу);
	ФС.ОбеспечитьКаталог(Файл.Путь);
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(Файл.ПолноеИмя);
	ЗаписьXML.ЗаписатьНачалоЭлемента("coverage");
	ЗаписьXML.ЗаписатьАтрибут("version", "1");
	
	КоличествоСтрок = 0;
	
	Для Каждого Модуль Из ДанныеПокрытия Цикл
		
		Если ПустаяСтрока(Модуль.ПутьКМодулю) Тогда
			Лог.Отладка("Не удалось определить путь к модулю объекта " + Модуль.ИдОбъекта + 
			" с типом модуля " + Модуль.ИдТипаМодуля);
			Продолжить;
		КонецЕсли;
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("file");
		ЗаписьXML.ЗаписатьАтрибут("path", Модуль.ПутьКМодулю);
		
		Для Каждого СтрокаМодуля Из Модуль.НомераСтрок Цикл
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("lineToCover");
			ЗаписьXML.ЗаписатьАтрибут("lineNumber", СтрокаМодуля.Ключ);
			ЗаписьXML.ЗаписатьАтрибут("covered", "true");
			ЗаписьXML.ЗаписатьКонецЭлемента();
			
			КоличествоСтрок = КоличествоСтрок + 1;
			
		КонецЦикла;
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();
	
	Лог.Отладка("Количество строк в замерах = " + КоличествоСтрок);

КонецПроцедуры
