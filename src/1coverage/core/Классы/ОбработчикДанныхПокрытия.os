Перем ДанныеЗамеров;

Перем Лог;

Процедура ПриСозданииОбъекта(пДанныеЗамеров) Экспорт

	Лог = ПараметрыПриложения.Лог();

	ДанныеЗамеров = пДанныеЗамеров;

КонецПроцедуры

Функция ЗаполнитьДанныеПокрытия() Экспорт

	ДанныеПокрытия = ДанныеПокрытияИнициализация();

	Для Каждого ДанныеМодулейВЗамерах Из ДанныеЗамеров Цикл

		Если ТипЗнч(ДанныеМодулейВЗамерах) = Тип("Массив") Тогда
			Для Каждого ДанныеМодуляВЗамерах Из ДанныеМодулейВЗамерах Цикл
				ЗаполнитьДанныеМодуля(ДанныеМодуляВЗамерах, ДанныеПокрытия);
			КонецЦикла;
		Иначе
			ДанныеМодуляВЗамерах = ДанныеМодулейВЗамерах;
			ЗаполнитьДанныеМодуля(ДанныеМодуляВЗамерах, ДанныеПокрытия);
		КонецЕсли;
		
	КонецЦикла;

	Возврат ДанныеПокрытия;

КонецФункции

Функция ДанныеПокрытияИнициализация()

	ДанныеПокрытия = Новый ТаблицаЗначений();
	ДанныеПокрытия.Колонки.Добавить("ИдМодуля");
	ДанныеПокрытия.Колонки.Добавить("ИдТипаМодуля");
	ДанныеПокрытия.Колонки.Добавить("ИмяФайла");
	ДанныеПокрытия.Колонки.Добавить("ПутьКОбъекту");
	ДанныеПокрытия.Колонки.Добавить("ИмяКоманды");
	ДанныеПокрытия.Колонки.Добавить("ПутьКМодулю");
	ДанныеПокрытия.Колонки.Добавить("НомераСтрок");

	Возврат ДанныеПокрытия;

КонецФункции

Процедура ЗаполнитьДанныеМодуля(ДанныеМодуля, ДанныеПокрытия)

	ИдМодуля = ДанныеМодуля.moduleID.objectID._text;
	ИдТипаМодуля = ДанныеМодуля.moduleID.propertyID._text;
	
	НайденнаяСтрокаМодуль = ДанныеПокрытия.Найти(ИдМодуля, "ИдМодуля");
	Если НайденнаяСтрокаМодуль <> Неопределено Тогда
		ТекСтрокаМодуль = НайденнаяСтрокаМодуль;
	Иначе
		ТекСтрокаМодуль = ДанныеПокрытия.Добавить();
		ТекСтрокаМодуль.НомераСтрок = Новый Соответствие();
	КонецЕсли;
	ТекСтрокаМодуль.ИдМодуля = ИдМодуля;
	ТекСтрокаМодуль.ИдТипаМодуля = ИдТипаМодуля;
	
	Для Каждого СтрокаПокрытия Из ДанныеМодуля.lineInfo Цикл
		НомерСтрокиМодуля = СтрокаПокрытия.lineNo._text;
		Если ТекСтрокаМодуль.НомераСтрок[НомерСтрокиМодуля] <> Неопределено Тогда
			ТекущееКоличествоХитов = ТекСтрокаМодуль.НомераСтрок[НомерСтрокиМодуля] + 1;
		Иначе
			ТекущееКоличествоХитов = 1;
		КонецЕсли;
		
		ТекСтрокаМодуль.НомераСтрок.Вставить(СтрокаПокрытия.lineNo._text, ТекущееКоличествоХитов);
		
	КонецЦикла;
	
	Лог.Отладка("В замерах обнаружен модуль с id " + ИдМодуля);

КонецПроцедуры
